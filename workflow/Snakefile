from os.path import join
import pandas as pd
from snakemake.utils import validate

configfile: 'config/config.yml'
validate(config, "schemas/config.schema.yml")

#load participants.tsv file, and strip off sub- from participant_id column
df = pd.read_table(config['participants_tsv'])
validate(df, 'schemas/participants.schema.yml')

subjects = df.participant_id.to_list() 
subjects = [ s.strip('sub-') for s in subjects ]

#this include is for the bids() function, and 
#and any other global function declarations
include: 'rules/common.smk'

rule all:
    input:
        reg_vis = expand(bids(root='qc',subject='{subject}',suffix='regqc.png',from_='subject', to='{template}',desc='{desc}'),
                subject=subjects, desc=['affine','SyN'],template=config['template']),
        electrode_vis = expand(bids(root='qc',subject='{subject}',suffix='electrodevis.png',desc='{desc}',space='{template}'),
                subject=subjects, desc=['affine'],template=config['template']),
        probseg_vis = expand(bids(root='qc',subject='{subject}',suffix='probseg.png', desc='atropos3seg'),
                subject=subjects ),
        atlas_vis = expand(bids(root='qc',subject='{subject}',suffix='dseg.png',atlas='{atlas}', from_='{template}'),
                subject=subjects, atlas=config['atlases'],template=config['template']),
        tsv = expand(bids(root='results',subject='{subject}',suffix='electrodes.tsv',atlas='{atlas}',desc='dilated',from_='{template}'),
                subject=subjects, atlas=config['atlases'],template=config['template']),
        results = ['results.tar', 'results_tsv_only.tar']
                
       
rule tar_results:
    input:
        tsv = expand(bids(root='results',subject='{subject}',suffix='electrodes.tsv',atlas='{atlas}',desc='dilated',from_='{template}'),
                subject=subjects, atlas=config['atlases'],template=config['template']),
        dseg_nii = expand(bids(root='results',subject='{subject}',suffix='dseg.nii.gz',desc='dilated',atlas='{atlas}',from_='{template}',reg='SyN'),
                subject=subjects, atlas=config['atlases'],template=config['template']),
        tissue_seg = expand(bids(root='results',subject='{subject}',suffix='probseg.nii.gz',label='{tissue}',desc='atropos3seg'),
                            tissue=config['tissue_labels'],subject=subjects),
        t1w = expand(bids(root='results',subject='{subject}',suffix='T1w.nii.gz',from_='atropos3seg',desc='masked'),
                    subject=subjects)

    output: 'results.tar'
    shell: 'tar -cvf {output} {input}'

rule tar_tsv_only:
    input:
        tsv = expand(bids(root='results',subject='{subject}',suffix='electrodes.tsv',atlas='{atlas}',desc='dilated',from_='{template}'),
                subject=subjects, atlas=config['atlases'],template=config['template']),
    output: 'results_tsv_only.tar'
    shell: 'tar -cvf {output} {input}'


include: "rules/registration.smk"
include: "rules/segmentation.smk"
include: "rules/electrodes.smk"
include: "rules/visqc.smk"
